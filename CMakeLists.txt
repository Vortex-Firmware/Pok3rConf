cmake_minimum_required(VERSION 3.0)

project(Pok3rConf)

add_subdirectory(pok3rtool)

set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC ON)

find_package(Qt5 COMPONENTS Core Quick Qml QuickWidgets Widgets REQUIRED)

qt5_wrap_ui(UI_SRC
    mainwindow.ui
    editor/keycustomize.ui
)

qt5_add_resources(RC_SRC
    resource.qrc
)

set(SRC
    main.cpp
    mainwindow.h
    mainwindow.cpp
    mainworker.h
    mainworker.cpp
    editor/keycustomize.h
    editor/keycustomize.cpp
    ${UI_SRC}
    ${RC_SRC}
)

include_directories(
    ${CMAKE_BINARY_DIR}
    ${Qt5Widgets_INCLUDE_DIRS}
    ${QtQml_INCLUDE_DIRS}
)

GitDescribe("${CMAKE_CURRENT_SOURCE_DIR}" POK3RCONF_DESCRIBE)
message(STATUS "Pok3rConf: ${POK3RCONF_DESCRIBE}")
set_source_files_properties(main.cpp PROPERTIES COMPILE_DEFINITIONS _POK3RCONF_DESCRIBE="${POK3RCONF_DESCRIBE}")

if(WIN32)
    add_executable(pok3rconf WIN32 ${SRC})
else()
    add_executable(pok3rconf ${SRC})
endif()
#qt5_use_modules(pok3rconf Quick Core)
target_link_libraries(pok3rconf chaos-shared pok3rlib Qt5::Widgets Qt5::Quick Qt5::Qml Qt5::QuickWidgets)

# install
install(TARGETS pok3rconf DESTINATION bin)

# copy shared libraries to build dir
add_custom_command(TARGET pok3rconf POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        "$<TARGET_FILE:chaos-shared>"
        "$<TARGET_FILE_DIR:pok3rconf>"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        "$<TARGET_FILE:rawhid>"
        "$<TARGET_FILE_DIR:pok3rconf>"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        "$<TARGET_FILE:pok3rlib>"
        "$<TARGET_FILE_DIR:pok3rconf>"
)

if(WIN32)
    add_custom_command(TARGET pok3rconf POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "$<TARGET_FILE:Qt5::Core>"
            "$<TARGET_FILE_DIR:pok3rconf>"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "$<TARGET_FILE:Qt5::Gui>"
            "$<TARGET_FILE_DIR:pok3rconf>"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "$<TARGET_FILE:Qt5::Widgets>"
            "$<TARGET_FILE_DIR:pok3rconf>"
    )
endif()

if(MINGW)
    # Apparently Qt's mingw doesn't come with some static libraries
    get_filename_component(MINGW_DIR ${CMAKE_CXX_COMPILER} DIRECTORY)
    set(MINGW_LIBS_DIR "${MINGW_DIR}/../i686-w64-mingw32/lib")
    set(MINGW_LIBGCC "${MINGW_LIBS_DIR}/libgcc_s_dw2-1.dll")
    set(MINGW_LIBSTD "${MINGW_LIBS_DIR}/libstdc++-6.dll")
    set(MINGW_LIBWPT "${MINGW_LIBS_DIR}/libwinpthread-1.dll")

    add_custom_command(TARGET pok3rconf POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "${MINGW_LIBGCC}"
            "$<TARGET_FILE_DIR:pok3rconf>"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "${MINGW_LIBSTD}"
            "$<TARGET_FILE_DIR:pok3rconf>"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "${MINGW_LIBWPT}"
            "$<TARGET_FILE_DIR:pok3rconf>"
    )
endif()

set(QMK_DIR "${CMAKE_SOURCE_DIR}/qmk_firmware")

if(NOT WIN32)
    set(QMK_POK3R       "${QMK_DIR}/vortex_pok3r_chaos.bin")
    set(QMK_POK3R_RGB   "${QMK_DIR}/vortex_pok3r_rgb_chaos.bin")
    set(QMK_VORTEX_CORE "${QMK_DIR}/vortex_core_chaos.bin")

    add_custom_target(firmware ALL
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/qmk_firmware
        COMMAND make vortex/pok3r:chaos
        COMMAND make vortex/pok3r_rgb:chaos
        COMMAND make vortex/core:chaos
    )

    # install firmware
    install(FILES "${QMK_POK3R}"        DESTINATION share/pok3rconf)
    install(FILES "${QMK_POK3R_RGB}"    DESTINATION share/pok3rconf)
    install(FILES "${QMK_VORTEX_CORE}"  DESTINATION share/pok3rconf)
endif()

# generated header file
GitDescribe("${QMK_DIR}" QMK_DESCRIBE)
configure_file(gen_firmware.in.h gen_firmware.h)

# uninstall target
if(NOT TARGET uninstall)
    configure_file(
        "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
        "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
        IMMEDIATE @ONLY)

    add_custom_target(uninstall
        COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake)
endif()
